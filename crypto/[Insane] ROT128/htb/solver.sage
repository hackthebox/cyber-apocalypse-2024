from pwn import process, remote
from random import randint
from Crypto.Util.number import long_to_bytes as l2b, bytes_to_long as b2l
import itertools, math

ROUNDS = 3
N = 128
F.<w> = GF(2**N)
PR.<z> = PolynomialRing(GF(2))
R = lambda x, i : ((x << i) | (x >> (N-i))) & (2**N - 1)

def int2pre(i):
    coeffs = list(map(int, bin(i)[2:].zfill(N)))[::-1]
    return PR(coeffs)

def pre2int(p):
    coeffs = p.coefficients(sparse=False)
    return sum(2**i * int(coeffs[i]) for i in range(len(coeffs)))

def get_server_message_and_hash(io):
    io.recvuntil(b'H(')
    server_msg = int(io.recv(64), 16)
    io.recvuntil(b' = ')
    server_hash = l2b(int(io.recvline().strip().decode(), 16))
    return server_msg, server_hash

def extract_m1_m2(server_msg):
    m1 = server_msg >> N
    m2 = server_msg & (2**N - 1)
    return m1, m2

def compute_H1_H2(server_msg, server_hash):
    m1, m2 = extract_m1_m2(server_msg)
    H1 = b2l(server_hash[:16]) ^^ m1
    H2 = b2l(server_hash[16:]) ^^ m2
    return H1, H2

def get_all_possible_candidates():
    powers = '0123456789'
    cands = itertools.product(powers, repeat=2)
    d = {}
    for cand in set(cands):
        r2 = int(cand[0])
        r4 = int(cand[1])
        s = 2**r2+2**r4
        d[s] = sorted([r2, r4])
    return d

def extract_r1_r3_candidates(numer):
    numer_factors = sorted([F(i**j).to_integer() for i,j in list(numer.factor())])
    cands = []
    for factor in numer_factors:
        r1 = int(math.log2(factor))
        if 2**r1 == factor:
            r3 = r1
            cands.append((r1, r3))
    return cands

def extract_r2_r4_candidate(B, d, visited):
    factors = sorted([F(i**j).to_integer() for i,j in list(B.factor())])

    for fact in factors:
        if fact in visited:
            continue

        if fact in d:
            r2, r4 = d[fact]
            visited.append(fact)
            return (r2, r4)

def run_task(io, d, used_states, visited):
    server_msg, server_hash = get_server_message_and_hash(io)

    if len(server_hash) < 32:
        return None

    H1, H2 = compute_H1_H2(server_msg, server_hash)

    B = int2pre(H1) + int2pre(H2)

    if not B:
        return None
    
    r2_r4_cand = extract_r2_r4_candidate(B, d, visited)

    if not r2_r4_cand:
        return None
      
    r2, r4 = r2_r4_cand

    assert B.mod(int2pre(2**r2 + 2**r4)) == 0

    y = B / int2pre(2**r2 + 2**r4)
    numer = int2pre(H1) - y * int2pre(2**r2)

    r1_r3_cands = extract_r1_r3_candidates(numer)

    if not r1_r3_cands:
        return None

    for (r1, r3) in r1_r3_cands:
        x = numer / int2pre(2**r1)
        x = pre2int(PR(x))
        y = pre2int(PR(y))

        if sorted([r1, r2, r3, r4]) in used_states:
            continue

        if H1 == R(x, r1) ^^ R(y, r2) and H2 == R(x, r3) ^^ R(y, r4):
            used_states.append(sorted([r1, r2, r3, r4]))
            state = r1, r2, r3, r4, x, y
            return state

def send_state(io, r1, r2, r3, r4, x, y):
    io.sendlineafter(b' :: ', f'{r1},{r2},{r3},{r4},{x},{y}'.encode())

def pwn():
    d = get_all_possible_candidates()
    
    while True:
        used_states = []
        visited = []
        done = 0
        io = remote('localhost', '1337', level='error')
        for _ in range(ROUNDS):
            state = run_task(io, d, used_states, visited)
            if state:
                r1, r2, r3, r4, x, y = state
                send_state(io, r1, r2, r3, r4, x, y)
                done += 1
                print(f'round {done} done!')
                
                if done == ROUNDS:
                    io.recvline()
                    print(io.recvline().decode())
                    exit()
            else:
                print('fail!')
                io.close()
                break


if __name__ == '__main__':
    pwn()